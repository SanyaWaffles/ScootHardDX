/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

class SHDX_AtmosphereHandler : EventHandler
{
	SHDX_DrAtmosphereBoss dratmos;
	SHDX_MaimbowUndashBoss mboss;
	Array<SHDX_ZombieNormal> zombies;
	
	int zombietics;
	
	const TIMETOSPAWNZOMBIES = (TICRATE * 15);
	const TIMETOCOPYARRAY = (TICRATE * 10);
	
	override void WorldTick()
	{
		// BossInfoTick();
		PruneZombiesFromArray();
		DrAtmosphereRoutine();
		MaimbowRoutine();
	}
	
	override void WorldThingSpawned(WorldEvent e)
	{
		CheckIfSpawnedZombie(e.Thing);
		CheckIfSpawnedAtmosphere(e.Thing);
		CheckIfSpawnedMaimbow(e.Thing);
	}
	
	override void WorldThingDied(WorldEvent e)
	{
		CheckIfKilledZombie(e.Thing);
	}
	
	void CheckIfSpawnedZombie(Actor a)
	{
		let z = SHDX_ZombieNormal(a);
		if (z)
		{
			zombies.Push(z);
		}
	}
	
	void CheckIfSpawnedAtmosphere(Actor a)
	{
		let at = SHDX_DrAtmosphereBoss(a);
		if (at)
		{
			dratmos = at;
		}
	}
	
	void CheckIfSpawnedMaimbow(Actor a)
	{
		let m = SHDX_MaimbowUndashBoss(a);
		if (m)
		{
			mboss = m;
		}
	}
	
	void DrAtmosphereRoutine()
	{
		// If Maimbow is present, don't bother as he has
		// presidence...
		if (mboss)
			return;
		
		if (dratmos)
		{
			if (dratmos.isactive)
			{
				zombietics++;
				if (zombietics >= TIMETOSPAWNZOMBIES)
				{
					SpawnZombiesFromSpots();
					zombietics = 0;
				}
				if (dratmos.health <= 0)
				{
					KillZombies();
					dratmos = null;
				}
			}
		}
	}
	
	void MaimbowRoutine()
	{
		if (mboss)
		{
			if (mboss.isactive)
			{
				zombietics++;
				if (zombietics >= TIMETOSPAWNZOMBIES)
				{
					SpawnZombiesFromSpots();
					zombietics = 0;
				}
				if (mboss.health <= 0)
				{
					KillZombies();
					mboss = null;
				}
			}
		}
	}
	
	void KillZombies()
	{
		ThinkerIterator zombieit = ThinkerIterator.Create("SHDX_ZombieNormal");
		SHDX_ZombieNormal zom;
		
		// iterate through the spots
		while (zom = SHDX_ZombieNormal(zombieit.Next()))
		{
			// if spot exists
			if (zom)
			{
				zom.A_Die('Extreme');
			}
		}
	}
	
	void SpawnZombiesFromSpots()
	{
		ThinkerIterator deadraiserit = ThinkerIterator.Create("SHDX_ZombieSpot");
		SHDX_ZombieSpot raiserspot;
		
		// iterate through the spots
		while (raiserspot = SHDX_ZombieSpot(deadraiserit.Next()))
		{
			// if spot exists
			if (raiserspot)
			{
				raiserspot.SetStateLabel("RaiseTheDead");
			}
		}
	}
	
	void CheckIfKilledZombie(Actor a)
	{
		let z = SHDX_ZombieNormal(a);
		if (z)
		{
			int s = zombies.Find(z);
			
			if (s >= zombies.Size())
				return;
				
			zombies.Delete(s);
		}
	}
	
	void PruneZombiesFromArray()
	{
		if (level.time % TIMETOCOPYARRAY == 0)
		{
			int size = zombies.Size();
			
			if (size < 1)
				return;
			
			if (zombies.Find(null) >= size)
				return;
			
			Array<SHDX_ZombieNormal> tempitems;
			for (int i = 0; i < size; i++)
			{
				if (zombies[i] && zombies[i].health > 0)
				{
					tempitems.Push(zombies[i]);
				}
			}
			
			zombies.Move(tempitems);
		}
	}
}