/*
Copyright (c) 2020 Marisa Kirisame, UnSX Team

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

// Save version holder
Class SHDX_SaveVersionData : Thinker
{
	String ver;
}

Class SHDX_ErrorHandler : StaticEventHandler
{
	ui bool wasinmap;
	ui int timer;
	bool tainted;
	String taintver;
	
	override void NewGame()
	{
		let svd = new("SHDX_SaveVersionData");
		svd.ChangeStatNum(Thinker.STAT_STATIC);
		svd.ver = StringTable.Localize("$SHDX_BUILDNUM");
	}
	
	override void WorldLoaded(WorldEvent e)
	{
		if (!e.IsSaveGame) return;
		let ti = ThinkerIterator.Create("SHDX_SaveVersionData", Thinker.STAT_STATIC);
		let svd = SHDX_SaveVersionData(ti.Next());
		if (!svd)
		{
			tainted = true;
			taintver = "\cg"..StringTable.Localize("$SHDX_NOVERSION").."\c-";
			Console.PrintF("\cg"..StringTable.Localize("$SHDX_WARNING").." \cj"..StringTable.Localize("$SHDX_LOADEDNOVERDATA"));
			return;
		}
		String cver = StringTable.Localize("$SHDX_BUILDNUM");
		if (svd.ver != cver)
		{
			tainted = true;
			taintver = svd.ver;
			Console.PrintF("\cg"..StringTable.Localize("$SHDX_WARNING").." \cj"..StringTable.Localize("$SHDX_LOADEDDIFFERENTDATA"));
			Console.PrintF("\cgSaved:   \cj"..svd.ver);
			Console.PrintF("\cgCurrent: \cj"..cver);
		}
	}
	
	// The nukkus detector is mine, not Marisa's
	ui bool nukkusdetected;
	
	ui void NukkusRoutine()
	{
		if (nukkusdetected || gamestate != GS_LEVEL)
			return;
		
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			String un = players[i].GetUserName();
			if (un ~== "nukkus" || un ~== "haruka mishima")
			{
				Console.PrintF(StringTable.Localize("$SHDX_NUKKUSWARN"));
				nukkusdetected = true;
			}
		}
	}
	
	ui void CrashRoutine()
	{
		if ( (gamestate == GS_LEVEL) || (gamestate == GS_TITLELEVEL) )
		{
			wasinmap = true;
			timer = 0;
		}
		else if ( (gamestate == GS_FULLCONSOLE) && ((wasinmap && !players[consoleplayer].viewheight) || (timer > 0)) )
		{
			wasinmap = false;
			if ( timer == 1 )
			{
				Console.Printf("\cf"..StringTable.Localize("$SHDX_CRASHMESSAGE"));
				
				S_StartSound("mac/crash",9999,CHANF_UI|CHANF_NOPAUSE|CHANF_OVERLAP,1,ATTN_NONE);
			}
			else if ( timer == 140 )
			{
				Console.Printf("\cf"..StringTable.Localize("$SHDX_CRASHHARAM"));
			}
			else if ( timer == 350 )
			{
				Console.Printf("\cf"..StringTable.Localize("$SHDX_CRASHNOBRUTALDOOM"));
			}
			timer++;
		}
	}

	override void UiTick()
	{
		NukkusRoutine();
		CrashRoutine();
	}
	
	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name ~== "triggercrash" && e.isManual)
		{
			if (players[e.Player].mo)
			{
				players[e.Player].mo.ThrowAbortException(StringTable.Localize("$SHDX_MANUALCRASHABORT"));
			}
		}
	}
}