/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


Class SHDX_DrAtmosphereBoss : SHDX_Boss
{
	Default
	{
		//$Category "Monsters"
		//$Sprite ATMOA0
		//$Title "Boss 2 - Dr Atmosphere"
		
		Health 1750;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 12;
		PainChance "Explosive", 256;
		PainChance "Bullet", 256;
		DamageFactor "Bullet", 0.05;
		DamageFactor "Explosive", 0.75;
		DamageFactor "Chicken", 0.00;
		DamageFactor "ChickenX", 0.00;
		DamageFactor "Rainbower", 0.10;
		DamageFactor "RainbowerX", 0.10;
		DamageFactor "Ice", 0.00;
		SeeSound "boss2/sight";
		PainSound "boss2/pain";
		DeathSound "boss2/death";
		Obituary "$SHDX_OB_DRATMOS";
		Tag "$SHDX_TAG_DRATMOS";
		
		+DONTRIP
		+NOICEDEATH
		
		
		+SHDX_Boss.TriggerLevelEnd
		+SHDX_Boss.TriggerCutscene
		
		SHDX_Boss.CutsceneToPlay 'EndEpisode2';
	}
	
	States
	{
		Spawn:
			ATMO A 10 A_Look;
			Loop;
		See:
			ATMO BCDE 2 A_Chase;
			ATMO BCDE 2 A_Chase;
			ATMO BCDE 2 A_Chase;
			ATMO BCDE 2 A_Chase;
			ATMO BCDE 2 A_Chase;
			ATMO BCDE 2 A_Chase;
			ATMO B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_BOSS_CHANCE, "atmospherestart", OLSSF_FORCEONELINER);
		See1:
			ATMO BCDE 2 A_Chase;
			Loop;
		Missile:
			ATMO F 0 A_Jump(96, "Missile1");
			ATMO F 5 A_FaceTarget;
			ATMO G 0 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO GH 2;
			ATMO G 0 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO GH 2;
			ATMO G 0 Bright A_SpawnProjectile("SHDX_AtmosphereHailBall");
			ATMO GH 2;
			goto See1;
		Missile1:
			ATMO IJK 2 A_FaceTarget;
			ATMO A 0 A_SpawnItemEx("SHDX_DrAtmosFx1");
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, 0);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, -8);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, 8);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, -16);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, 16);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, -32);
			ATMO A 0 A_SpawnProjectile("SHDX_AtmosphereHailBall", 48, 0, 32);
			ATMO K 5 Bright;
			goto See1;
		Pain:
			ATMO F 3
			{
				A_Pain();
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			ATMO F 2
			{
				SetShade("f08010");
			}
			ATMO F 1
			{
				SetShade("ffee6a");
			}
			ATMO F 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			Goto See1;
		Death.Explosive:
		Death.Weather:
		Death.RainbowX:
		XDeath:
		Death:
			ATMO L 0 A_NewScream;
			ATMO LMNLMNLMN 5;
			ATMO O 5 A_NoBlocking;
			ATMO P 5 A_SpawnGibsFx(self);
			ATMO Q 5;
			ATMO R 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_BOSS_CHANCE, "atmosphereend", OLSSF_FORCEONELINER);
			ATMO R 220;
			ATMO R 1 A_DD2_BossPlayCutscene();
			ATMO R -1 A_DD2_BossEndLevel();
			Stop;
	}
}

Class SHDX_DrAtmosFx1 : SHDX_Actor
{
	States
	{
		Spawn:
			ATFX A 0 A_FadeTo(0.0, 0.1, true);
			ATFX A 0 A_SetScale((scale.X+0.1), scale.Y);
			ATFX ABC 3;
			loop;
	}
}

Class SHDX_AtmosphereHailBall : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 4;
		Projectile;
		+RANDOMIZE
		DamageType "Ice";
		SeeSound "weapons/hailf";
		DeathSound "weapons/hailx";
	}
	States
	{
		Spawn:
			HAIL A 1;
			Loop;
		Death:
			HAIL B 2;
			Stop;
	}
}