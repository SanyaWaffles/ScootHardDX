/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


Class SHDX_PegasusDevice : SHDX_Boss
{
	Default
	{
		//$Category "Monsters"
		//$Sprite PDP1A0
		//$Title "Boss 3 - Pegasus Device"
		Health 3000;
		Radius 56;
		Height 116;
		Mass 100000;
		Speed 12;
		DamageFactor "Bullet", 0.05;
		DamageFactor "Explosive", 0.45;
		DamageFactor "Chicken", 0.00;
		DamageFactor "ChickenX", 0.00;
		DamageFactor "Rainbower", 0.10;
		DamageFactor "RainbowerX", 0.10;
		DamageFactor "Ice", 0.00;
		SeeSound "boss3/sight";
		PainSound "boss3/pain";
		DeathSound "boss3/death";
		ActiveSound "boss3/active";
		Obituary "$SHDX_OB_PEGASUSDEVICE";
		Tag "$SHDX_TAG_PEGASUSDEVICE";
		
		+NOBLOOD
		+DONTRIP
		+NOICEDEATH
		+NOPAIN
		
		+SHDX_Boss.TriggerLevelEnd
		+SHDX_Boss.TriggerCutscene
		
		SHDX_Boss.CutsceneToPlay 'EndEpisode3';
	}
	
	States
	{
		Spawn:
			PDP1 A 10 A_Look;
			Loop;
		See:
			PDP1 BCDE 2 A_Chase;
			PDP1 BCDE 2 A_Chase;
			PDP1 B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_BOSS_CHANCE, "pegasusdevice", OLSSF_FORCEONELINER);
		See1:
			PDP1 BCDE 2 A_Chase;
			goto See1;
		Melee:
			PDP1 F 5 A_FaceTarget;
			PDP1 FGFGFG 5 A_CustomMeleeAttack(5, "boss3/meleehit", "boss3/meleemiss");
			goto See1;
		Missile:
			PDP1 H 0 A_JumpIfHealthLower(750, "Missile1");
			PDP1 H 5 A_FaceTarget;
			PDP1 I 0 Bright A_SpawnProjectile("SHDX_PDPRainbowBall");
			PDP1 IJK 2;
			PDP1 H 3 A_FaceTarget;
			PDP1 I 0 Bright A_SpawnProjectile("SHDX_PDPRainbowBall");
			PDP1 IJK 2;
			PDP1 H 2 A_FaceTarget;
			PDP1 I 0 Bright A_SpawnProjectile("SHDX_PDPRainbowBall");
			PDP1 IJK 2;
			goto See1;
		Missile1:
			PDP1 I 0 A_FaceTarget;
			PDP1 I 0 A_SpawnProjectile("SHDX_PDPRainbowBall");
			PDP1 IJK 5 Bright;
			goto Missile1;
		Death.Explosive:
		Death.Weather:
		Death.RainbowX:
		XDeath:
		Death:
			PDP1 A 0 A_Scream;
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 8, 8, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 0, 0, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", -8, -8, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 8, 8, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 0, 0, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", -8, -8, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 8, 8, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", 0, 0, 8);
			PDP1 A 5 Bright A_SpawnItemEx("SHDX_Explosiveness", -8, -8, 8);
			PDP1 L 5 A_NoBlocking;
			PDP1 MNO 5;
			PDP1 O 220;
			PDP1 O 1 A_DD2_BossPlayCutscene();
			PDP1 O -1 A_DD2_BossEndLevel();
			Stop;
	}
}

Class SHDX_Explosiveness : SHDX_Actor
{
	States
	{
		Spawn:
			EXP2 A 0 NoDelay A_StartSound("world/barrelx", CHAN_BODY);
			EXP2 ABCDEFGHIJK 3 bright A_Stop;
			stop;
	}
}

Class SHDX_PDPRainbowBall : SHDX_Projectile
{
	Default
	{
		Radius 11;
		Height 6;
		Speed 20;
		Damage 12;
		PROJECTILE;
		DamageType "RainbowerX";
		DeathSound "weapons/rainbowerx";
	}
	
    States
    {
		Spawn:
			RBWP AB 2 Bright;
			Loop;
		Death:
			RBWN BCD 3;
			stop;
    }
}


Class SHDX_PDPRainbowBall2 : SHDX_PDPRainbowBall
{
	Default
	{
		Scale 1.2;
		Speed 24;
		Damage 20;
	}
}