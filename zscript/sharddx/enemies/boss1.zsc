/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_BossSpot : SpecialSpot
{
	Default
	{
		//$Category "Monsters"
		//$Sprite internal:action
		//$Title "Boss Teleport Spot"
		
		+Invisible
	}
}

Class SHDX_SurpriseBoss : SHDX_Boss
{
	Default
	{
		//$Category "Monsters"
		//$Sprite SURPA1
		//$Title "Boss 1 - Ms. Surprise"
		
		Health 1200;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 0;
		
		PainChance "Explosive", 256;
		PainChance "Bullet", 128;
		PainChance "Rainbower", 256;
		PainChance "RainbowerX", 256;
		PainChance "Ice", 64;
		DamageFactor "Bullet", 0.50;
		DamageFactor "Explosive", 0.75;
		DamageFactor "Chicken", 0.00;
		DamageFactor "ChickenX", 0.00;
		DamageFactor "Rainbower", 0.10;
		DamageFactor "RainbowerX", 0.10;
		DamageFactor "Ice", 0.35;
		// SeeSound "boss1/sight";
		PainSound "boss1/pain";
		DeathSound "boss1/death";
		// ActiveSound "boss1/active";
		Obituary "$SHDX_OB_SURPRISE";
		Tag "$SHDX_TAG_SURPRISE";
	
		+DONTRIP
		+NOICEDEATH
		
		
		+SHDX_Boss.TriggerLevelEnd
		+SHDX_Boss.TriggerCutscene
		
		SHDX_Boss.CutsceneToPlay 'EndEpisode1';
	}
	
	bool floorlowered;
	bool isactive;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		SeeSound = "boss1/sight"..Random[Surprise](1,3);
	}
	
	void A_SurpriseSetBossMusic()
	{
		S_ChangeMusic("M_SURP");
	}
	
	void A_SurpriseSetBossVictoryMusic()
	{
		S_ChangeMusic("M_SURPV", 0, false);
	}
	
	void A_SurpriseTaunt()	
	{
		A_SHDX_PlayBossTaunt("boss1/taunt"..Random[Surprise](1,6));
	}
	
	override void MarkPreCacheSounds()
	{
		Super.MarkPreCacheSounds();
		Sound snd;
		for (int i = 1; i < 7; i++)
		{
			snd = String.Format("boss1/taunt%d", i);
			MarkSound(snd);
		}
	}
	
	void A_SurpriseLowerFloor()
	{
		// ACS_NamedExecute("MAP03_PeelAway", 0, 0, 0, 0);
		SectorTagIterator sit = Level.CreateSectorTagIterator(667);
		int i;
		while (i = sit.Next())
		{
			if (i == -1)
				break;
			
			Sector s = level.Sectors[i];
			
			s.SetTexture(Sector.floor, TexMan.CheckForTexture("GSPECF0", TexMan.Type_Flat));
			level.CreateFloor(s, Floor.FloorLowerByValue, null, 8, 128);
			s.SetGlowColor(Sector.floor, "00FF00");
			s.SetGlowHeight(Sector.floor, 64);
		}
		// Floor_LowerByValueTimes8(667,35,8);
		floorlowered = true;
	}
	
	States
	{
		Spawn:
			SURP AA 10
			{
				A_Look();
				isactive = false;
			}
			Loop;
		See:
			SURP A 0
			{
				A_SetInvulnerable();
				A_StartSound(SeeSound, CHAN_VOICE, attenuation:ATTN_NONE);
				A_SurpriseSetBossMusic();
				bCanJump = false;
			}
			SURP A 35 A_FaceTarget();
		See1:
			SURP A 0
			{
				// A_SHDX_SendOLToAllPlayers("surprisestart", OLSSF_FORCEONELINER);
				// A_UnSetInvulnerable();
				A_SHDX_SetToBossBarHUD();
				isactive = true;
			}
		See2:
			SURP A 20
			{
				A_SHDX_Chase(null, null, CHF_DONTMOVE);
				A_FaceTarget();
			}
		TeleportAboutInitial:
			SURP D 2 A_SetTranslucent(0.5, 0);
			SURP D 1;
			SURP D 0 A_Teleport("JustTeleportedInitial", "SHDX_BossSpot", "SHDX_NoFlash", 0);
			SURP D 1;
			Goto See3;
		JustTeleportedInitial:
			SURP D 0 A_UnSetInvulnerable();
			SURP D 0 A_SpawnItemEx("SHDX_TeleportFog", flags:SXF_NOCHECKPOSITION);
			SURP D 3 A_SetTranslucent(1.0, 0);
			SURP D 3 A_StartSound("boss1/active", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
			goto Missile;
		See3:
			SURP A 20
			{
				A_SHDX_Chase(null, null, CHF_DONTMOVE);
				A_FaceTarget();
			}
			SURP A 0 A_Jump(96, "TeleportAbout");
			Loop;
			
		Missile:
			SURP B 3 A_FaceTarget;
			SURP C 5
			{
				A_StartSound("boss1/stomp", CHAN_WEAPON);
				A_SpawnProjectile("SHDX_SurpriseTornado");
			}
			SURP E 0 A_SurpriseTaunt;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			SURP EF 3;
			goto TeleportAbout;
		JustTeleported:
			SURP D 0 A_SpawnItemEx("SHDX_TeleportFog", flags:SXF_NOCHECKPOSITION);
			SURP D 3 A_SetTranslucent(1.0, 0);
			SURP D 3 A_StartSound("boss1/active", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
			SURP D 3 A_Jump(128, "TeleportAbout");
			goto Missile;
		TeleportAbout:
			SURP D 2 A_SetTranslucent(0.5, 0);
			SURP D 1;
			SURP D 0 A_Teleport("JustTeleported", "SHDX_BossSpot", "SHDX_NoFlash", 0);
			SURP D 1;
			Goto See3;
		Pain.Bullet:
		Pain.Explosive:
		Pain.Rainbower:
		Pain.RainbowerX:
		Pain.Ice:
			SURP A 0
			{
				if (!floorlowered && health <= (SpawnHealth() / 2))
				{
					return ResolveState("SeeFloorPeelaway");
				}
				return ResolveState(null);
			}
			SURP H 5
			{
				A_StartSound("boss1/pain"..Random[Surprise](1,3), CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			SURP H 5
			{
				SetShade("f08010");
			}
			SURP H 5
			{
				SetShade("ffee6a");
			}
			SURP H 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			Goto TeleportAbout;
			
		SeeFloorPeelaway:
			SURP H 5
			{
				bNoPain = true;
				A_SetInvulnerable();
				A_StartSound("boss1/pain", CHAN_VOICE, attenuation:ATTN_BOSSTAUNT);
				A_SetRenderStyle(1.0, STYLE_Stencil);
				SetShade("6e0600");
			}
			SURP H 5
			{
				SetShade("f08010");
			}
			SURP H 5
			{
				SetShade("ffee6a");
			}
			SURP H 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
			}
			SURP H 0
			{
				A_StartSound("boss1/surprisefloorpeel", CHAN_VOICE, attenuation:ATTN_NONE);
				bCanJump = false;
			}
			SURP H 220;
			SURP H 0
			{
				A_SurpriseLowerFloor();
				A_UnSetInvulnerable();
				bCanJump = true;
				bNoPain = false;
			}
			Goto TeleportAbout;
			
		Death.Explosive:
		Death.Weather:
		Death.RainbowX:
		XDeath:
		Death:
			SURP H 0
			{
				A_SetRenderStyle(1.0, STYLE_Normal);
				SetShade("ffffff");
				A_NewBossScream();
				A_SurpriseSetBossVictoryMusic();
			}
			SURP H 105;
			SURP I 5 A_NoBlocking;
			SURP J 5 A_SpawnGibsFx(self);
			SURP KLM 5;
			SURP M 100;
			SURP M 0 A_SHDX_SendOLToAllPlayers("surpriseend", OLSSF_FORCEONELINER);
			SURP M 125;
			SURP M 1 A_DD2_BossPlayCutscene();
			SURP M -1 A_DD2_BossEndLevel();
			Stop;
	}
}

Class SHDX_SurpriseTornado : SHDX_Projectile
{
	Default
	{
		Radius 16;
		Height 56;
		Speed 8;
		Damage 10;
		DamageType "Weather";
		+MISSILE
		+DROPOFF
		+RIPPER
		+NOEXPLODEFLOOR
		+STEPMISSILE
		MaxStepHeight 16;
		Projectile;
		+SEEKERMISSILE
		// +FLOORHUGGER
	}
	States
	{
		Spawn:
			TORN A 0
			{
				A_SeekerMissile(1, 15);
				A_StartSound("boss1/tornado", CHAN_BODY);
			}
			TORN ABCD 1;
			Loop;
		Death:
			TORN A 3
			{
				A_FadeTo(0.0, 0.1, true);
			}
			TORN BCD 3;
			loop;
	}
	
	override void Tick()
	{
		Super.Tick();
		if (target.health <= 0 && !InStateSequence(CurState, ResolveState("Death")))
			SetStateLabel("Death");
	}
	
	// From Iron Lich...
	override int DoSpecialDamage (Actor thingtodmg, int damage, Name damagetype)
	{
		int randVal;

		if (!thingtodmg.bDontThrust)
		{
			thingtodmg.angle += Random2[TornadoDamage]() * (360 / 4096.);
			thingtodmg.Vel.X += Random2[TornadoDamage]() / 64.;
			thingtodmg.Vel.Y += Random2[TornadoDamage]() / 64.;
		}

		if ((Level.maptime & 16) && !thingtodmg.bBoss && !thingtodmg.bDontThrust)
		{
			randVal = min(160, random[WhirlwindSeek]());
			thingtodmg.Vel.Z += randVal / 32.;
			if (thingtodmg.Vel.Z > 12)
			{
				thingtodmg.Vel.Z = 12;
			}
		}
		if (!(Level.maptime & 4))
		{
			thingtodmg.DamageMobj (self.target, self.target, 15, 'Weather');
		}
		return -1;
	}
}

Class SHDX_RocketGuardSpot : SHDX_Actor
{
	Default
	{
		//$Category "Monsters"
		//$Sprite ZMCLB0
		//$Title "Surprise Rocket Guard Spawning Spot"
		Radius 16;
		Height 56;
		
		+NOBLOCKMAP
	}
	
	States
	{
		Spawn:
			TNT1 A 4;
			Loop;
		SpawnInRocketGuard:
			TNT1 A 0
			{
				A_SpawnItemEx("SHDX_TeleportFog", flags:SXF_NOCHECKPOSITION);
				A_SpawnItemEx("SHDX_RocketGuard", flags:SXF_NOCHECKPOSITION);
			}
			Goto Spawn;
	}
}