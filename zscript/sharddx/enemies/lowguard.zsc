/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


Class SHDX_LowGuardSpawner : RandomSpawner
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD1F0
		//$Title "Low Guard (Random)"
		scale 0.12;
		Radius 20;
		Height 56;
		DropItem "SHDX_LowGuardMale", 255, 7;
		DropItem "SHDX_LowGuardFemale", 255, 13;
	}
}

Class SHDX_LowGuardMale : SHDX_Enemy
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD1A0
		//$Title "Low Guard (Male)"
		SeeSound "grunt1/sight";
		PainSound "grunt1/pain";
		DeathSound "grunt1/death";
		ActiveSound "grunt1/active";
		Health 25;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		Obituary "$SHDX_OB_LOWGUARD";
		Tag "$SHDX_TAG_LOWGUARD";
	}
	
	bool A_CheckDiscourseDrop()
	{
		if (G_SkillPropertyInt(SKILLP_ACSReturn) < 3)
			return true;
			
		if (G_SkillPropertyInt(SKILLP_ACSReturn) >= 3 && Random[LowGuard](0, 100) >= 50)
			return true;
		
		return false;
	}
	
	void A_DropItemsLowGuard()
	{
		if (target && target.CountInv("SHDX_BitCoin") < 50 && A_CheckDiscourseDrop())
		{
			A_SHDX_DropItem("SHDX_SilverBitCoin", false);
		}
	}
	
	States
	{	
		Spawn:
			GRD1 A 10 A_Look;
			loop;
		See:
			GRD1 BCDEBCDE 4 A_Chase;
			Loop;
		Missile:
			GRD1 F 10 A_FaceTarget;
			GRD1 G 8 bright
			{
				A_StartSound("grunt/attack", CHAN_WEAPON);
				A_CustomBulletAttack(1, 0.0, 1, random[LowGuard](1,5)*1, "SHDX_BulletPuff", 0, CBAF_NORANDOM);
			}
			GRD1 H 4;
			Goto See;
		Pain:
			GRD1 I 0
			{
				if (Random[LowGuard](0, 100) < 50)
					return ResolveState("Pain2");
				
				return ResolveState("Pain1");
			}
		Pain1:
			GRD1 I 3;
			GRD1 I 3 A_Pain;
			Goto See;
		Pain2:
			GRD1 J 3;
			GRD1 J 3 A_Pain;
			Goto See;
		Death:
			GRD1 K 5 A_NewScream;
			GRD1 L 5
			{
				A_NoBlocking();
				A_DropItemsLowGuard();
			}
			GRD1 M 5;
			GRD1 N -1 A_SetCorpseShootable;
			Stop;
		Death.Explosive:
		Death.Weather:
		Death.Extreme:
		XDeath:
			GIBX A 3
			{
				A_SHDX_ExtremeScreamAndUnblock();
				A_DropItemsLowGuard();
				A_SpawnGibsFx(self);
			}
			GIBX B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBX BC 5;
			GIBX D -1;
			Stop;
		Death.RainbowerX:
			GIBR A 4;
			GIBR B 0 A_SHDX_GivePlayerOneLiner(SHDX_ONELINER_GIB_CHANCE);
			GIBR BC 4;
			GIBR D -1;
			Stop;
		Crush:
			GIBX D 0 A_SHDX_SquishSound;
			GIBX D 1 A_SHDX_GivePlayerOneLiner;
			GIBX D -1;
			Stop;
	}
}

Class SHDX_LowGuardFemale : SHDX_LowGuardMale
{
	Default
	{
		//$Category "Monsters"
		//$Sprite GRD2A0
		//$Title "Low Guard (Female)"
		SeeSound "grunt2/sight";
		PainSound "grunt2/pain";
		DeathSound "grunt2/death";
		ActiveSound "grunt2/active";
	}
	
	States
	{	
		Spawn:
			GRD2 A 10 A_Look;
			loop;
		See:
			GRD2 BCDEBCDE 4 A_Chase;
			Loop;
		Missile:
			GRD2 F 10 A_FaceTarget;
			GRD2 G 8 bright
			{
				A_StartSound("grunt/attack", CHAN_WEAPON);
				A_CustomBulletAttack(1, 0.0, 1, random[LowGuard](1,5)*1, "SHDX_BulletPuff", 0, CBAF_NORANDOM);
			}
			GRD2 H 4;
			Goto See;
		Pain:
			GRD2 I 0
			{
				if (Random[LowGuard](0, 100) < 50)
					return ResolveState("Pain2");
				
				return ResolveState("Pain1");
			}
		Pain1:
			GRD2 I 3;
			GRD2 I 3 A_Pain;
			Goto See;
		Pain2:
			GRD2 J 3;
			GRD2 J 3 A_Pain;
			Goto See;
		Death:
			GRD2 K 5 A_NewScream;
			GRD2 L 5
			{
				A_NoBlocking();
				A_DropItemsLowGuard();
			}
			GRD2 M 5;
			GRD2 N -1 A_SetCorpseShootable;
			Stop;
	}
}