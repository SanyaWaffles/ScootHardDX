/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_Bit : Inventory
{
	Default
	{
		Radius 10;
		Height 16;
		Scale 0.12;
		Inventory.PickupSound "pickup/generic";
		+Inventory.IgnoreSkill
	}
	
	mixin PickupSoundOverride;
	
	// This is basically how the ammo from GZDoom is handled
	// but for our purposes...
	
	Class<SHDX_Bit> GetParentBit()
	{
		Class<Object> type = GetClass();
		
		while (type.GetParentClass() != "SHDX_Bit" && type.GetParentClass() != null)
		{
			type = type.GetParentClass();
		}
		
		return (Class<SHDX_Bit>)(type);
	}
	
	override Inventory CreateCopy (Actor other)
	{
		SHDX_Bit copy;
		int amount = Amount;
		
		Class<Object> o = GetClass();
		
		let type = GetParentBit();
		if (GetClass() != type && type != null)
		{
			if (!GoAway())
				Destroy();
				
			copy = SHDX_Bit(Spawn(type));
			copy.Amount = amount;
			copy.BecomeItem();
		}
		else
		{
			copy = SHDX_Bit(Super.CreateCopy(other));
			copy.Amount = amount;
		}
		
		if (copy.Amount > copy.MaxAmount)
		{
			copy.Amount = copy.MaxAmount;
		}
		return copy;
	}
	
	override bool HandlePickup (Inventory item)
	{
		let bitcoin = SHDX_Bit(item);
		if (bitcoin != null && bitcoin.GetParentClass() == GetClass())
		{
			if (Amount < MaxAmount)
			{
				int recieving = bitcoin.amount;
				
				if (Amount > 0 && Amount + recieving < 0)
				{
					Amount = 0x7fffffff;
				}
				else
				{
					Amount += recieving;
				}
				if (Amount > MaxAmount)
				{
					Amount = MaxAmount;
				}
				
				item.bPickupGood = true;
			}
			return true;
		}
		return false;
	}
}

Class SHDX_BitCoin : SHDX_Bit
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 50;
	}
}

Class SHDX_SilverBitCoin : SHDX_BitCoin
{
	Default
	{
		//$Sprite BIT1A0
		//$Title "1 Bits"
		//$Category "Health and Armor"
		Tag "$SHDX_TAG_1BIT";
		Inventory.Amount 1;
		Inventory.PickupMessage "$SHDX_PICKUP_1BIT";
	}
	
	States
	{
		Spawn:
			BIT1 A -1;
			Stop;
	}
}

Class SHDX_GoldBitCoin : SHDX_BitCoin
{
	Default
	{
		//$Sprite BIT5A0
		//$Title "5 Bits"
		//$Category "Health and Armor"
		Tag "$SHDX_TAG_5BIT";
		Inventory.Amount 5;
		Inventory.PickupMessage "$SHDX_PICKUP_5BIT";
	}
	
	States
	{
		Spawn:
			BIT5 A -1;
			Stop;
	}
}