/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/


Class SHDX_ScootalooPlayer : SHDX_PlayerPawn
{
	Default
	{
		TeleFogSourceType "SHDX_TeleportFog";
		TeleFogDestType "SHDX_TeleportFog";
		
		Scale 0.14;
		
		Speed 1;
		Health 100;
		Radius 16;
		Height 34;
		Mass 100;
		PainChance 255;
		
		Friction 0.995;
		
		BloodType "SHDX_Blood", "SHDX_Blood", "SHDX_Blood";
		
		Player.MaxHealth 100;
		
		Player.DisplayName "Scootaloo";
		Player.StartItem "SHDX_Blaster";
		Player.SoundClass "scootaloo";
		
		Player.WeaponSlot 1, "SHDX_Blaster";
		Player.WeaponSlot 2, "SHDX_SubmachineGun";
		Player.WeaponSlot 3, "SHDX_MissileLauncher", "SHDX_FillySeeker", "SHDX_Tornadozer", "SHDX_Rainbower", "SHDX_HailerWeapon";

		Player.ColorRange 240, 254;
		Player.ColorSet 0, "$SHDX_SCOOTALOO_COLORSET1", 250, 240, 254;
		Player.ColorSet 1, "$SHDX_SCOOTALOO_COLORSET2", 24, 16, 31;
		Player.ColorSet 2, "$SHDX_SCOOTALOO_COLORSET3", 54, 48, 63;
		Player.ColorSet 3, "$SHDX_SCOOTALOO_COLORSET4", 88, 80, 95;
		Player.ColorSet 4, "$SHDX_SCOOTALOO_COLORSET5", 120, 112, 127;
		Player.ColorSet 5, "$SHDX_SCOOTALOO_COLORSET6", 150, 144, 159;
		Player.ColorSet 6, "$SHDX_SCOOTALOO_COLORSET7", 180, 176, 190;
		Player.ColorSet 7, "$SHDX_SCOOTALOO_COLORSET8", 100, 96, 111;
		
		Player.DamageScreenColor "FF0000", 0.70;
		Player.DamageScreenColor "FFA500", 0.70, "Explosive";
		Player.DamageScreenColor "F0509A", 0.70, "Warp";
		Player.DamageScreenColor "FF5800", 0.50, "Fire";
		Player.DamageScreenColor "CD7F32", 0.70, "Expand";
		Player.DamageScreenColor "00FF00", 0.70, "Shrink";
		Player.DamageScreenColor "0047AB", 0.70, "Webbing";
		Player.DamageScreenColor "00FF00", 0.70, "Bleach";
		Player.DamageScreenColor "00FF00", 0.70, "Cancer";
		Player.DamageScreenColor "00FF00", 0.70, "Acid";
		
		Player.TeleportFreezeTime 0;
		
		Player.ForwardMove 2, 1;
		Player.SideMove 2, 1;
		
		Player.ViewHeight 26;
		Player.AttackZOffset 8;
		Player.JumpZ 12;
		
		Player.AirCapacity 0;
		
		+NoSkin
		
		SHDX_PlayerPawn.OneLinerPrefix "scootaloo";
	}
	
	override void Tick()
	{
		// to avoid something going on with "voodoo dolls" not performing right
		// or the like.
		
		if (!player || !player.mo || player.mo != self)
		{
			// Only perform the PlayerPawn functionality, nothing more...
			Super.Tick();
			return;	// bailing out...
		}
		
		Super.Tick();	// call any PlayerPawn Tick functionality
	}
	
	override void MovePlayer()
	{
		let player = self.player;
		UserCmd cmd = player.cmd;

		// [RH] 180-degree turn overrides all other yaws
		if (player.turnticks)
		{
			player.turnticks--;
			Angle += (180. / TURN180_TICKS);
		}
		else
		{
			Angle += cmd.yaw * (360./65536.);
		}

		player.onground = (pos.z <= floorz) || bOnMobj || bMBFBouncer || (player.cheats & CF_NOCLIP2);

		// killough 10/98:
		//
		// We must apply thrust to the player and bobbing separately, to avoid
		// anomalies. The thrust applied to bobbing is always the same strength on
		// ice, because the player still "works just as hard" to move, while the
		// thrust applied to the movement varies with 'movefactor'.

		if (cmd.forwardmove | cmd.sidemove)
		{
			double forwardmove, sidemove;
			double bobfactor;
			double friction, movefactor;
			double fm, sm;

			[friction, movefactor] = GetFriction();
			bobfactor = friction < ORIG_FRICTION ? movefactor : ORIG_FRICTION_FACTOR;
			
			if (!player.onground && !bNoGravity && !waterlevel)
			{
				// [RH] allow very limited movement if not on ground.
				movefactor *= 0.25;
				bobfactor*= 0.25;
			}

			fm = cmd.forwardmove;
			sm = cmd.sidemove;
			[fm, sm] = TweakSpeeds (fm, sm);
			fm *= Speed / 256;
			sm *= Speed / 256;

			// When crouching, speed and bobbing have to be reduced
			if (CanCrouch() && player.crouchfactor != 1)
			{
				fm *= player.crouchfactor;
				sm *= player.crouchfactor;
				bobfactor *= player.crouchfactor;
			}

			forwardmove = fm * movefactor * (35 / TICRATE);
			sidemove = sm * movefactor * (35 / TICRATE);

			if (forwardmove)
			{
				Bob(Angle, cmd.forwardmove * bobfactor / 256., true);
				ForwardThrust(forwardmove, Angle);
			}
			if (sidemove)
			{
				let a = Angle - 90;
				Bob(a, cmd.sidemove * bobfactor / 256., false);
				Thrust(sidemove, a);
			}

			if (!(player.cheats & CF_PREDICTING) && (forwardmove != 0 || sidemove != 0))
			{
				PlayRunning ();
			}

			if (player.cheats & CF_REVERTPLEASE)
			{
				player.cheats &= ~CF_REVERTPLEASE;
				player.camera = player.mo;
			}
			
			
			if (player && !player.onground && vel.z > 0)
			{
				if (player.cmd.buttons & BT_JUMP && player.jumptics <= -6)
				{
					gravity = 0.3;
				}
				else
				{
					gravity = GetDefaultByType("SHDX_ScootalooPlayer").Gravity;
				}
			}
		}
	}
	
	override void MarkPreCacheSounds()
	{
		Sound snd;
		for (int i = 1; i < 11; i++)
		{
			snd = String.Format("scootaloo/voiceover%d", i);
			MarkSound(snd);
		}
	}
	
	States
	{
		Spawn:
			SCOO A 6;
			Loop;
			
		See:
			SCOO BCDE 4;
			Loop;
		
		Melee:
		Missile:
			SCOO FGH 2 BRIGHT;
			Goto Spawn;
			
		Pain:
			SCOO I 4;
			SCOO I 4 A_NewPlayerPain;
			Goto Spawn;
		
		Death:
		Death.Fire:
			SCOO J 5
			{
				A_StopOneLinerSound();
				DDFunc_PlayerDies();
			}
			SCOO KL 3;
			SCOO M -1;
			Stop;
		
		Death.Extreme:
		Death.Explosive:
			TNT1 A 0
			{
				A_StopOneLinerSound();
				DDFunc_PlayerDiesXTreem();
			}
			GIBX A 4 A_XScream();
			GIBX BC 4;
			GIBX D 4;
			GIBX E 0 A_PlayerScream();
			GIBX E -1;
			Stop;
		
		Crush:
			GIBX E -1;
			Stop;
	}
}
