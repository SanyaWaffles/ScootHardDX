/*
Copyright 2020 Sanya K. Waffles

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Class SHDX_TornadozerAmmo : Ammo
{
	Default
	{
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 5;
	}
}

Class SHDX_Tornadozer : SHDX_MissileWeapon
{
	Default
	{
		//$Title "Tornadozer"
		//$Sprite TGUNA0
		
		Tag "$SHDX_TAG_TORNADOZER";
		Weapon.SelectionOrder 500;
		Inventory.PickupMessage "$SHDX_PICKUP_TORNADOZER";
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOAUTOAIM
		+WEAPON.CHEATNOTWEAPON
		Weapon.AmmoGive 5;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "SHDX_TornadozerAmmo";
		Obituary "$SHDX_OB_TORNADOZER";
	}
	
	States
	{
		Ready: 
			TORG A 1 A_WeaponReady;
			LOOP;
		Deselect: 
			TORG A 1 A_Lower;
			LOOP;
		Select:
			TORG A 1 A_Raise;
			Loop;
		Flash:
			TNT1 A 1 A_Light1;
			goto LightDone;
		Fire: 
			TORG A 3;
			TORG A 3;
			TORG A 2 A_FireProjectile("SHDX_TornadozerProjectile");
			TORG A 5 A_ReFire;
			Goto Ready;
		Spawn:
			TGUN A -1;
			Loop;
	}
}

Class SHDX_TornadozerProjectile : SHDX_Projectile
{
	Default
	{
		Radius 10;
		Height 50;
		Speed 8;
		DamageType "Weather";
		Damage 5;
		Projectile;
		+MISSILE
		+DROPOFF
		+RIPPER
		+NOEXPLODEFLOOR
		+STEPMISSILE
		+SEEKERMISSILE
		+FLOORHUGGER
		MaxStepHeight 16;
	}
	
	int counter;
	
	const TICS_TILL_DISSIPATION = 260;
	
	States
	{
		Spawn:
			TORN A 0 NoDelay A_StartSound("weapons/tornadof", CHAN_BODY);
		Spawn1:
			TORN ABCD 1 A_SeekerMissile(0, 35, SMF_LOOK, 256);
			TORN A 0
			{
				counter += 4;
				
				if (counter >= TICS_TILL_DISSIPATION)
					return ResolveState("Death");
					
				return ResolveState(null);
			}
			Loop;
		Death:
			TORN A 3
			{
				A_FadeTo(0.0, 0.1, true);
			}
			TORN BCD 3;
			Loop;
	}
}